objclass:scenery
	attr:visible
objclass:sceneryobj
	attr:visible+listable
objclass:normalobj
	attr:visible+takeable+listable+large
objclass:actor
	attr:actor+visible+listable
verbclass:genericverb
	withobj:$none
		msg:\V what?
	withobj:$unknown
		msg:You don't see that
	withobj:$every
		ifobjin:$this,$available
			msg:You don't find a reason to do that
		else
			msg:You don't see it here	
verbclass:movementverb
	msg:You can't go that way
movementverb:u
	syn:u,up,go up
movementverb:d
	syn:d,down,go down
movementverb:s
	syn:s,south,go south
movementverb:n
	syn:n,north,go north
movementverb:w
	syn:w,west,go west
movementverb:e
	syn:e,east,go east
movementverb:se
	syn:se,southest,south-east,go southeast
movementverb:sw
	syn:sw,southwest,south-west,go southwest
movementverb:ne
	syn:ne,northeast,north-east,go northeast
movementverb:nw
	syn:nw,nortwest,north-west,go northwest
genericverb:x
	syn:x,examine,look,l,look at
	withobj:$none
		msg:$roomdesc
		ifobjinattr:$any,$here,visible+listable
			msg:You notice: +
			listobjin:$here,visible+listable
	withobj:$every
		ifobjin:$this,$available
			ifundef:$objdesc
				msg:Looks absolutely normal
			else
				msg:$objdesc
		else
			msg:You don't see that here
genericverb:take
	syn:take,get,pick
	withobj:$every
		ifobjin:$this,$here
			ifobj:$this,takeable+visible
				putobj:$this,$inventory
				msg:Taken
			else
				msg:You don't want to take it
		else
			ifobjin:$this,$inventory
				msg:You already have it
			else
				msg:You don't see it here
genericverb:drop
	syn:drop,put down
	withobj:$every
		ifobjin:$this,$inventory
			ifobj:$this,takeable+visible
				putobj:$this,$here
				msg:Dropped
			else
				msg:You can't drop it
		else
			msg:You don't have it
genericverb:use
genericverb:push
genericverb:pull
genericverb:open
genericverb:close
genericverb:lock
genericverb:unlock
verb:$unknown
	msg:You think that's not a wise idea
verb:onfirst
	clear
	msg:$roomdesc
	ifobjinattr:$any,$here,visible+listable
		msg:You see: +
		listobjin:$here,visible+listable
verb:onenter
	clear
	msg:You are in +
	msg:$roomname
	msg:.
	ifobjinattr:$any,$here,visible+listable
		msg:You see: +
		listobjin:$here,visible+listable
verb:help
	msg:In this kind of games you can do actions simply writing short commands in \yVERB+OBJECT \wform.
	msg:With them you can \yEXAMINE \welements, \yTAKE \wor \yDROP \witems and so on.
	msg:There are also special commands like \yINVENTORY \wthat will show what you're carrying with you.
	syn:help,h
verb:inventory
	ifobjinattr:$any,$inventory,takeable
		msg:You are carrying: +
		listobjin:$inventory,takeable
	else
		msg:You aren't carrying anything.
	syn:inventory,i
verb:score
	msg:Your current score is +
	msgvar:score
verb:quit
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		quit
	syn:quit,q
verb:restart
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		start
verb:save
	save
	msg:Done
verb:load
	msg:Are you sure? (Y/N)
	getkey
	ifkey:Y
		load
verb:mem
	dbg
