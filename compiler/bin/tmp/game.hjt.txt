config
	name:Nesterin
	title:Nesterin Falls
	subtitle:A C64 fantasy text adventure
	author:Marco Giorgini
	version:0.1.6
	binary:yes
	image
		morecontrast:1
		unsharpen:2
		sharpen:2
		dither:3
		rborder:1
		hborder:8
include:stdlib.hjt
normalobj:knife
	name:your knife
	desc:It's a small hunter knife. 
		It's a weapon, and it's not legal anymore to go around with them without a permit or a serious reason.
	startin:$inventory
	verb:hide
	verb:drop
		msg:You can't drop it here. You need to find a place to hide it, that's also safe.
	verb:use
		withobj:schoolalley.door
			ifobjin:knife,$inventory
				ifnot:schoolpanelremoved
					msg:Using your knife, and not without effort, you manage to rise and move a panel enough to create an opening.
					set:schoolpanelremoved
normalobj:pendant
	name:a moon-shaped pendant
	desc:It's a small metallic pendant. Your guild secret recognition object.
	startin:$inventory
	verb:drop
		msg:You don't want to drop it.
normalobj:yourself
	attr:visible
	verb:x
		if:potionon
			msg:Hard to believe, now you look like a middle-aged Lyrin soldier. You're also wearing their light armour.
		else
			msg:You're a young guy, thin, but robust and quick. Your father was a Romir farmer, and your mother a Lyrin gypsy, and, after the invasion, that put you in a peculiar position.
	startin:$everywhere
normalobj:beerbarrel
	name:a beer barrel
	synonym:barrel,beer barrel
	attr:visible+takeable+listable+empty
	verb:x
		ifobjin:$this,$available
			msg:It's a medium sized barrel. +
			ifobj:$this,empty
				msg:Empty.
			else
				msg: Full of beer.
	verb:drop
		ifobjin:$this,$available
			msg:It's not yours to leave around.
	verb:drink
		ifobjin:$this,$available
			ifobj:$this,empty
				msg:It's empty.
			else
				msg:You take a sip. Nice.
	startin:$nowhere
genericverb:attack
	withobj:$none
		msg:Violence isn't wise, with so many enemies nearby
	withobj:$every
		ifobj:$this,actor+visible
			msg:Too risky.
		else
			ifobjin:$this,$available
				msg:What?
			else
				msg:You don't see that here	
genericverb:kill
	withobj:$none
		msg:Violence isn't wise, with so many enemies nearby
	withobj:$every
		ifobj:$this,actor+visible
			msg:Too risky.
		else
			ifobjin:$this,$available
				msg:What?
			else
				msg:You don't see that here	
topics:erik
room:$start
	image:png\nhome.png
	onfirst
		msg:\c##SUBTITLE##
			\c
			\cCode+Story: \y##AUTHOR##
			\c
			\c\gVersion: ##VERSION##
		waitkey
		clear
		msg:You are the youngest member of the Garrot Guild, or what is left of it. Most of those loyal to the Duke were killed after the Lyrin invasion.
		msg:But your master's research did not stop. And it is more important than ever.
		msg:You know little about it. But you know how to move and blend in, thanks in part to your mixed race.
		msg:So you were not surprised when you were sent to Nesterin, one of the settlements close to the old border, to get hold of Erik's report, whatever it might contain.
		msg:You have long since lost hope of a breakthrough that would give the small resistance a chance of victory, but not the will to fight back.
		waitkey
		goto:mainsquare
town
room:mainsquare
	name:Main square
	image:png\nvillage2.png
	onfirst
		clear
		msg:You arrived at this remote town at dawn. There aren't many people around, and every face you see bears the scars of having been oppressed.
		msg:Your goal now is to find Erik. Not as easy as it sounds, considering you've never met him. All you know about him is that he's a northern man with black hair and green eyes, arrived here six months ago.
	desc:Even if Nesterin is no longer an important silver mining town, its main @square is still impressive. 
		Many of the @shops seem to have gone out of business, but the @buildings still look rich and elegant, and around the @fountain some @people are chatting quietly on the @benches.
		The southern @gate can be seen from here. There's a @inn to the east, and large roads to the west and north seem to lead further into the town.
		{inn:The sign says 'The Silver Fox'. Looks inviting.}
		{square:It's a beautiful place.}
		{shops:Most of the shops in this square are silver-releated, but many are closed, and the other seems to be out of stock.}
		{fountain:The fountain is small but functional. From time to time people come to drink.}
		{buildings:The buildings here are mostly wooden, but one can see that this city is, or was, rich.}
		{benches:The benches are wooden, and although old, in good condition.}
		{gate:It's the gate you entered Nesterin from. Open, with a single silent guard that didn't stop you or asked you anything.}
		{guards:Here there are mainly local guards. They seem tired and bored, but they're neverthless armed.}
	attr.shortdesc:in Nesterin Main square
	attr.exits:[east,south,west,north]
	scenery:people
		desc:Most of the people around are women or elderly men. Or @guards.
		verb:ask
		verb:talk
			msg:They seem afraid, and not willing to interact with you.
	verb:e
		goto:innhall
	verb:s
		goto:southtownentrance
	verb:n
		goto:bankssquare
	verb:w
		goto:market
	verb:go|enter
		withobj:inn
			goto:innhall
room:bankssquare
	name:Banks square
	image:png\statuesquare.png
	desc:Although not as large as the main square, it is even more amazing. Every @building around it is made of stone, and there's a huge white @statue in the centre, representing a man holding a strange @instrument.
		If you go south from here, you can head towards the entrance to the city, while if you go east, you'll reach the governor's quarters.
		{buildings:Most of the buildings in this place are banks, or loan offices.}
		{statue:The statue is very tall and rests on a massive square base covered with @inscriptions. The man depicted is supposed to be one of the founders of the city.}
		{instrument:It is an elongated object. You don't know what it is and you've never seen one before.}
		{inscriptions:The inscriptions tell the heroic story of the first settlers, who fought nature and adversity. Above those writings there is a huge, convoluted layout whose meaning is beyond your comprehension.}
	attr.exits:[east,south]
	scenery:buildings
		synonym:building,buildings
	scenery:statue
	scenery:instrument
	verb:s
		goto:mainsquare
	verb:e
		ifobjin:knife,$inventory
			msg:A little further on you see some guards and you know that if they find you with the hunting knife you will be in trouble. Better hide it if you want to move around.
		else
			goto:governorsquare
room:governorsquare
	name:Governor's square
	image:png\nmajorsquare.png
	desc:This beautiful @square with a huge @fountain leads to the Governor's @Palace. Even if his role after the invasion is just to follow orders and collect taxes, the invaders didn't remove him, but they did add @guards from their ranks. And there are plenty of them here, armed and dangerous.
		To the north is the @prison, from which you can still hear horrible screams, while to the west is the square with the statue.
	attr.exits:[west,northwest,north]
	scenery:square
	scenery:fountain
	scenery:palace
	scenery:guards
		synonym:guard,guards
	verb:w
		goto:bankssquare
	verb:n
		goto:prison
	verb:nw
		goto:northtownentrance
room:prison
	name:Prison
	image:png\prison.png
	desc:A stone building with metal doors. Even this @room, the entrance to what must be a huge prison, with even underground cells, is unsettling because of the screams and the smell.
		The exit door is to the west, while to the north there's a reception @desk with a large @door behind it.
	attr.shortdesc:inside the prison
	attr.exits:[south]
	scenery:door
	scenery:desk
	actor:prisonguard
		name:a prison @guard
		synonym:guard
	verb:s
		goto:governorsquare
room:market
	name:Market
	image:png\nmarket.png
	desc:The market @square is relatively small, with some closed @stalls.
		The ones that are open offer a variety of @food and @clothes, but few customers seem to be able to buy anything.
		From here it's possible to walk to the entrance of the town, which lies to the east. 
		To the north, there's a @building that catches your eye because of its boarded-up door.
		{building:It's a big building, with boarded-up doors.}
		{stalls:Sheets tied with ropes cover the closed benches. Near some of them there are old men, sit on the ground.}
		{food:The food on display looks good, but really expensive. And very little @meat for sale.}
		{clothes:The clothes are well made, although they look second-hand.}
		{meat:It's mainly pork, but there's also some small wild animal.}
	attr.shortdesc:at the market
	attr.exits:[east,north]
	scenery:school
		verb:x
			msg:It's an elegant two-storey building, boarded up. You don't think there's a way in thru the front door.
			if:schoolseenfromnearby
				msg:On the left, to the north-west, you noticed a small alley. Wonder if there's a second, less blocked door there.
				set:schoolalleyseen
	actor:foodvendor
		name:a food @vendor
		desc:The food vendor is a blond woman, middle-aged. She seems nice.
		synonym:vendor
		verb:talk
			msg:"Hello young boy, do you want something?", she asks smiling, but the smile vanishes as soon as you admit you've no money.
		verb:ask
			withobj:school
				msg:"They closed it some time ago. Not the worst thing they did." she said.
			withobj:erik
				msg:"I don't know anyone by that name"
			withobj:food
				msg:"Do you have any money?"
			withobj:meat
				msg:You ask why there's a meat shortage, and the vendor answers vaguely. "Problems with suppliers", she says.
				set:meatneeded
				if:rabbitsseen
					msg:"I've seen rabbits in the forest", you say.
					msg:"Well, if you catch some, I could buy them", she affirms.
			withobj:$every
				msg:"sorry, it's a complicated time, I don't feel like talking, above all if you're not here for buying"
	actor:clothesseller
		name:a clothes @seller
		synonym:seller
		desc:The clothes seller is an old woman, with a sad face.
		verb:talk
			msg:"Hello, is there anything you like?", she asks, without too many expectations.
		verb:ask
			withobj:school
				msg:"It's our @school," she said, "or I should say it was. It's been closed by them one months ago."
			withobj:erik
				msg:"I don't know anyone by that name"
			withobj:$every
				msg:"sorry, it's a complicated time, I don't feel like talking, above all if you're not here for buying"
			withobj:clothes
				msg:"Do you have any money?"
	verb:e
		goto:mainsquare
	verb:n
		msg:You walk a hundred meters north and reach an elegant two-storey building. It's a @school, but the main door has been boarded up and wooden panels cover some of the windows. It's impossible to enter, so after a moment, you go back.
		set:schoolseenfromnearby
	verb:nw
		if:schoolalleyseen
			goto:schoolalley
		else
			msg:You can't go that way
room:school
	name:School room
	image:png\schoolroom2.jpg
	desc:The room you're in is full of desks. There still are writing @material on them. 
		Teacher's desk is partially broken, and there's @blood all over it. Some @cabinets were piled up on the west side, as if to block access to @stairs to upper floor.
		To the south is the boarded entrance door and the @windows facing the square. To the east the smaller door you entered from.
		{material:There are some papers and pencils}
		{blood:There's a lot of it. Someone was killed here.}
		{cabinets:They're several, and heavy.}
		{stairs:They're behind the cabinets, so unreachable.}
		{windows:You can see market square between the cracks in the boards.}
	attr.shortdesc:inside a school room
	attr.exits:[east,south]
	verb:e
		goto:schoolalley
	verb:u
		if:cabinetsbypassed
			goto:schoollibrary
		else
			msg:You cannot reach the stairs.
	verb:s
		msg:The main entrance is boarded up. You can't leave from it.
room:schoollibrary
	name:School Library
	image:png\schoollibrary2.jpg
	desc:This is one of the largest @libraries you've ever seen. Some of the @books have been thrown on the @floor and torn up, but most of them are still in their place. And some look ancient.
	attr.shortdesc:inside the School Library
	attr.exits:[down]
	scenery:books
	scenery:floor
	verb:d
		goto:school
room:schoolalley
	name:School alley
	image:png\schoolalley.jpg
	desc:Every @window along this wall is also blocked. There's a service @door, still boarded up, but it seems less solid than the main entrance.
	attr.shortdesc:in an alley beside the school
	attr.exits:[southeast]
	verb:se
		goto:market
	verb:w
		if:schoolpanelremoved
			goto:school
		else
			msg: You can't go that way
	scenery:door
		verb:pull
			if:schoolpanelremoved
				msg:You have already managed to lift a panel.
			else
				msg:Some panels seem unstable, but you can't remove with with bare hands.
		verb:push
			msg:You try to do this, without getting any useful results.
		verb:enter
			if:schoolpanelremoved
				goto:school
			else
				msg:How?
		desc:It's boarded up, but it doesn't seem too solid. Some panel seems unstable.
	scenery:window
		desc:Every window is blocked by boards.
		verb:pull
			msg:You try to do this, without getting any useful results.
		verb:push
			msg:You try to do this, without getting any useful results.
	scenery:school
		verb:enter
			if:schoolpanelremoved
				goto:school
			else
				msg:How?
inn
room:innhall
	name:Inn dining room
	image:png\ntavern.png
	desc:It is a tidy and spacious @inn, quiet and almost empty.
		Behind the @counter, the @innkeeper, an old frail man, seems to be staring at the wall, with an expression of pain.
		In front of you, behind the @tables, is a @fireplace. Stairs lead up.
		This place must have been glorious in the old days.
		{inn:It's a big and cozy place.}
		{counter:The counter is old, but perfectly maintained.}
		{tables:Few customers are drinking beer, and speaking in a low voice.}
		{fireplace:The fireplace is off at the moment.}
	attr.shortdesc:inside the inn
	attr.exits:[up,west]
	verb:w
		goto:mainsquare
	verb:u
		if:payedroom
			goto:upcorridor
		else
			msg:"Hey! Where are you going?", shouts the innkeeper.
			msg:"Rooms are for paying hosts only!"
		endif
	verb:e
		if:allowedinthekitchen
			goto:kitchen
		else
			msg:You can't go that way
	topics:beer
	topics:"room"
	actor:owner
		synonym:innkeeper
		name:the @innkeeper
		desc:He is an old man, and there is probably something wrong with him. He seems to be struggling to move an arm.
		verb:talk
			withobj:$every
				msg:"I don't want to sound rude, but I don't want to talk to you", he said, "So, please order or leave".
		verb:ask
			withobj:beerbarrel
				ifobjin:beerbarrel,$inventory
					ifobj:beerbarrel,empty
						msg:"I've got a Lyrin barrel to fill."	
						msg:The innkeeper looks at you in a odd way, then he says to go in the kitchen.
						set:allowedinthekitchen
					else
						msg:He just look at you without any answer.
				else
					msg:He just look at you without any answer.
			withobj:beer
				msg:"It's a tenth", he said.
				msg:You answer by shaking your head.
				msg:"That's what I thought", he concluded.
			withobj:room
				msg:"It's 5 coin per night", he said.
				msg:You answer by shaking your head.
				msg:"That's what I thought", he concluded.
			withobj:erik
				msg:"Never heard that name", he said, looking suspicious.
			withobj:$every
				msg:"I don't want to sound rude, but I don't want to talk to you", he said, "So, please order or leave".
		verb:show|give
			withobj:beerbarrel
				ifobjin:beerbarrel,$inventory
					ifobj:beerbarrel,empty
						msg:"I've got a Lyrin barrel to fill."	
						msg:The innkeeper looks at you in a odd way, then he says to go in the kitchen.
						set:allowedinthekitchen
					else
						msg:He just look at you without any answer.
				else
					msg:He just look at you without any answer.
	scenery:kitchen
		verb:go|enter
			if:allowedinthekitchen
				goto:kitchen
			else
				msg:"Hey! Where are you going?", shouts the innkeeper.
room:upcorridor
	name:Corridor
	image:png\ntcorridor.png
	desc:The corridor on the first floor is clean and tidy, and there are several closed @rooms with solid @doors.
		The place's mining past, when rich merchants slept here during their business travels, is clearly over, but the owner still tries to keep everything as nice as it was.
	attr.shortdesc:on the first floor of the inn
	attr.exits:[down]
	verb:e
		goto:innroom
	verb:d
		goto:innhall
	scenery:rooms
		synonym:room,rooms
	scenery:doors
room:innroom
	name:Room
	image:png\troom.png
	desc:This is a far cry from an ordinary inn @room, and now it's probably just a shadow of what it should have been in better days. The one thing that is almost normal is the stray @bed, but the rest of the @furniture is far better than that of your master.
	attr.shortdesc:inside a room
	attr.exits:[west]
	scenery:bed
	scenery:forniture
	verb:w
		goto:upcorridor
room:kitchen
	name:Kitchen
	image:png\nkitchen.png
	desc:Like everything else, the kitchen is beautiful and well equipped. It takes a little effort to notice that most of the spice @jars are almost empty.
		A large @window door lets in daylight.
		{jars:Spices seem to be running out. This kitchen needs supplies.}
		{window:From there you can see the main square, and you notice that there's someone lying under a bench.}
	attr.shortdesc:in the Inn's kitchen
	attr.exits:[west]
	verb:w
		goto:innhall
	actor:cook
		name:the @cook
		desc:A fat old man, with a patch on one eye, but with a smiling face.
		verb:ask|talk
			withobj:$every
				msg:The cook smiles but doesn't say anything.
		verb:ask
			withobj:jars
				msg:"Yes, I need supplies.", he says.
			withobj:beerbarrel
				ifobjin:beerbarrel,$inventory
					ifobj:beerbarrel,empty
						msg:"Ah. They finished another one. I'll take care of it."
						msg:The cook takes the barrel, and fills it. 
						msg:"Here you are." he says once finished.
						unsetattr:beerbarrel,empty
					else
						msg:"I've filled it. You can take it to those bastards now."
				else
					msg:The cook smiles but doesn't say anything.
			withobj:erik
				msg:He seems esitant
		verb:show|give
			withobj:beerbarrel
				ifobjin:beerbarrel,$inventory
					msg:"Ah. They finished another one. I'll take care of it."
				else
					msg:The cook smiles but doesn't say anything.
	scenery:windowdoor
		synonym:door,window,window door
	scenery:jars
		synonym:jar,jars
	scenery:kitchen
forest
room:southtownentrance
	name:Nesterin South entrance
	image:png\ntownentrance.png
	desc:You're just outside the walled @entrance to Nesterin.
		From here you can see the southern @road that brought you here, and a similarly large one heading north-west.
		All around is a deep @forest, with several small @paths, the most beaten to the east.
		{entrance:The entrance is open. It does not appear to have been closed for some time.}
		{road:The road is large, but there are no signs of a constant passage of people or wagons.}
		{forest:The forest in front of you is dense and full of wide and tall trees.}
		{paths:Among the trees you glimpse a few narrow paths here and there, but you see no one around.}
	attr.shortdesc:at the South entrance
	attr.exits:[east,south,northwest,north]
	scenery:leg
		desc:His leg seems somehow broken. He probably hardly walks.
	topics:men
	topics:barrels
	actor:entranceguard
		name:a local @guard
		synonym:guard
		desc:It's the first young local person you've seen in a while. Tall and thin, with a short sword, he seems tired, and he has something wrong in one @leg.
		verb:ask|talk
			withobj:leg
				msg:"What's wrong with your leg", you ask.
				msg:"None of your business," he replies. But he looks anxious to the northern @road.
				set:legseen
			withobj:road
				if:barreldutyknow
					msg:You don't need to ask that. You know what to do.
				else
					if:legseen
						msg:"Is there something that worries you?", you ask.
						msg:"They asked me to... it's complicated. Go away", he replies.
					else
						msg:"You cannot stand here, come in or go away", he says.
			withobj:barrels
				if:barreldutyknow
					msg:You don't need to ask that. You know what to do.
				else
						if:barrelsseen
							msg:"Have you seen the armed men? I need to take their empty barrels to the Inn to fill the again with beer", he replies.
							msg:"But it's hard for me. You seem strong. Will you do that for me? I will return the favour somehow."
							msg:You think about that, and then you say you'll do it.
							set:barreldutyknow
						else
							msg:"You cannot stand here, come in or go away", he says.
			withobj:men
				if:armedmenseen
					msg:"Do you mean those armed men over there? They're blocking the road to the silver mines."
				else
					msg:"You cannot stand here, come in or go away", he says.
			withobj:erik
				msg:It seems risky to ask him about Erik
			withobj:$every
				if:barreldutyknow
					msg:You don't need to ask that. You know what to do.
				else
					msg:"You cannot stand here, come in or go away", he says.
	verb:n
		goto:mainsquare
	verb:s
		msg:You can't go back without having gathered Erik's information.
	verb:nw
		goto:mineroad
	verb:e
		goto:path
room:path
	image:png\nforestpath1.png
	name:Forest path
	desc:This forest of tall @trees and few small @animals seems to be full of well-trodden @paths, surrounded by rich @vegetation, but after a while most of them just stop, and the feeling of not knowing where to go grows. 
		You get the impression that if you're not lost yet, you will be soon.
		{trees:There are at least three different types of trees, distinguishable by shape or leaves. But you don't know the name.}
		{animals:From time to time you see a few @rabbits, but from the sounds you get the impression that there are also larger animals around.}
		{vegetation:This forest is full of very diverse plants, some of which are also very colourful. You would need help to recognise the possibly edible ones.}
		{paths:The paths begin and end without leading anywhere. Probably traces of paths erased by time.}
		{rabbits:They're cautious. You can hardly see them.}
	scenery:rabbits
		x
			msg:They're cautious. You can hardly see them.
			set:rabbitsseen
	verb:w
		goto:southtownentrance
	verb:e
	verb:s
	verb:n
		goto:darkpath
	verb:capture
	verb:take
		withobj:rabbits
			msg:You need an arc to get them. Or a trap.
	attr.exits:[east,south,west,north]
	attr.shortdesc:on a forest path
room:darkpath
	name:Forest path
	image:png\nforestpath2.png
	desc:This part of the forest looks a little different to the one just outside the town, but it is just as difficult to find your way around because of all the absurdly well-trodden @paths.
		You just keep on walking, feeling like you are going in circles. You will certainly need some hints on how to deal with this.
		{paths:The paths begin and end without leading anywhere. Probably traces of paths erased by time.}
	attr.exits:[east,south,west,north]
	attr.shortdesc:on a forest path
	scenery:paths
		synonym:path,paths
	verb:s
	verb:e
	verb:w
	verb:n
		goto:path
	verb:se
		if:knowdarkpathroad
			goto:house
		else
			goto:path
room:betweenpath
	name:Forest path
	image:png\nforestpath3.png
	desc:This part of the forest seems to have more defined @paths, although the sometimes very dense vegetation still hides the direction in which they are going.
		{path:You see a few path that you could follow, including a leading one.}
	attr.shortdesc:on a forest path
	attr.exits:[west,southwest,northeast]
	verb:sw
		goto:northtownentrance
	verb:ne
		goto:ruins
	verb:w
		goto:forestpond
room:mineroad
	name:Northern road
	image:png\mineroad.jpg
	desc:You're not too far from the town, but the @road here is patrolled by a small group of Lyrin armed @men whose intention seems to block the passage, more than check travellers.
		{road:The road narrows here and you can see the mountains. Next to the armed men is a @tarp with items on it.}
	attr.shortdesc:along the northern road
	attr.exits:[southeast,northwest]
	actor:men
		name:armed @men
		verb:talk
			withobj:$every
				msg:"Get out of here before we slit your throat.", says one of them.
		verb:ask
			withobj:barrels
				if:barreldutyknow
					ifobjin:beerbarrel,$inventory
						msg:You can't carry more than one at time
					else
						msg:"I'm here to take the empty barrels, to fill them up", you say.
						msg:"It was about time! Hurry up! We're thirsty."
						msg:You reach them and take one.
						put:beerbarrel,$inventory
				else
					msg:No way.
			withobj:$every
				msg:"Get out of here before we slit your throat.", says one of them.
		verb:x
			msg:They're a small group of heavily armed Lyrin men. Their indestructible light armor and their razor-like swords don't make them as scary as their cruel faces.
			set:armedmenseen
	scenery:tarp
		verb:x
			msg:On the tarp there are small @barrels, food, and blankets. These men won't move anytime soon.
			set:barrelsseen
	scenery:barrels
		verb:take
			if:barreldutyknow
				ifobjin:beerbarrel,$inventory
					msg:You can't carry more than one at time
				else
					msg:"I'm here to take the empty barrels, to fill them up", you say.
					msg:"It was about time! Hurry up! We're thirsty."
					msg:You reach them and take one.
					put:beerbarrel,$inventory
			else
				msg:Stealing barrels from these men isn't exactly wise.
		desc:They're just medium-sized beer barrels. Some are surely empty.
	verb:n
	verb:nw
		msg:As soon as you start walking towards them, one of the armed men shouts at you to stop. "Who are you? Where are you going?" he asks menacingly.
		msg:When he sees that you are a half-breed he relaxes a little, but nevertheless orders you to turn back immediately.
		set:armedmenseen
	verb:se
		goto:southtownentrance
	verb:ne
		if:knowroadtocamp
			goto:camp
		else
			msg:You can't go that way
room:camp
	name:Lyrin camp
	desc:You managed to approach the small @camp unnoticed. You can see just a few soldiers and all in the central @clearing, from where they should not see you. There are several @tents in front of you, but only two can probably be reached without risks, one @green and one @red.
	attr.exits:[southwest]
	image:png\camp2.jpg
	verb:sw
		goto:mineroad
	scenery:green
		x
			msg:There are a lot of objects
	scenery:red
		x
			msg:There's a corpse of a tortured man. 
room:house
	name:Forest house
	desc:In front of you stands a small, solid stone @house, almost completely hidden by trees. It looks inhabited and grass grows even between the cracks in the @walls. 
		The front @door only looks as if it has been approached.
	image:png\foresthouse.jpg
	verb:n
		goto:houseinterior
	verb:s
		goto:darkpath
	attr.shortdesc:in front of a house
	attr.exits:[south,north]
room:houseinterior
	name:Forest house interior
	image:png\tforesthouseinterior.jpg
	desc:The first @room you encounter, despite the large @windows, seems lit by a winter sky. There is a @fireplace, in front of a @table with a @bottle and a @glass half-filled, a @bookcase filled with @books, and above it, a @painting of an elderly woman with no eyes. Everything looks as if it has just been abandoned.
	attr.shortdesc:inside a house
	attr.exits:[east,south,west]
	verb:s
		goto:house
	verb:e
		msg:The door leading east is shut, and you can't open it.
	verb:w
		msg:The door leading west is shut, and you can't open it.
room:ruins
	name:Ruins
	image:png\ruins.jpg
	desc:In a @clearing among the trees is the remains of a low fortified @building, almost completely collapsed. Inside the @walls there is only @vegetation and @rubble, except for a small @block where a metal @door hides a @section that is still covered.
	attr.exits:[southwest]
	attr.shortdesc:inside a collapsed building
	verb:d
		goto:ruinsinterior
	verb:sw
		goto:betweenpath
room:ruinsinterior
	name:Ruins interior
	desc:Down a narrow @staircase you enter a large @hall, dimly lit by @cracks in the stone @ceiling. Again, the @floor is littered with @debris, and the only thing that seems intact is what looks like an @altar in the centre. All around you can see @openings, dark, leading to no one knows where.
	image:png\xruinsinterior.jpg
	attr.exits:[up,east,south,west,north]
	attr.shortdesc:in a underground hall
	verb:u
		goto:ruins
	verb:n
	verb:s
	verb:w
	verb:e
		msg:There's too dark to go that way
room:forestpond
	name:Forest pond
	image:png\tforestpond.png
	desc:You can see that the many small @puddles are somehow a @watercourse because of the many floating @plants that cover part of it. It is not until you get further north that you notice that the @stream is widening.
	verb:n
		goto:fall
	verb:e
		goto:betweenpath
	attr.exits:[east,north]
	attr.shortdesc:near a pond
room:northtownentrance
	name:Nesterin North gate
	image:png\northentrance.jpg
	attr.exits:[southeast,northeast]
	attr.shortdesc:at the North entrance
	verb:ne
		goto:betweenpath
	verb:se
		goto:governorsquare
mine
room:mountainpath
	name:Mountain path
	image:png\mountainpath.jpg
	desc:The @path continues up the @mountain, narrow but well marked, with clear, polished @stones. However, a cold, strong wind is blowing, making the ascent tiring, with the risk of a fatal fall.
	verb:ne
		goto:mineentrance
	verb:sw
		goto:fall
	attr.shortdesc:on a mountain path
	attr.exits:[southwest,northeast]
room:fall
	name:Nesterin Falls
	image:png\nesterinfalls.jpg
	desc:The sight of these two spectacular @waterfalls coming together is magical. The @lake in front of you has a strangely shimmering bottom and the @path around it seems to go up into the @mountains.
	verb:s
		goto:forestpond
	verb:ne
		goto:mountainpath
	attr.shortdesc:in front of two waterfalls
	attr.exits:[south,northeast]
room:mineentrance
	name:Mine entrance
	image:png\tmineentrance.jpg
	desc:The @entrance to this @tunnel is huge, ancient and frightening. You are not able to recognize the @symbols on the @columns, so you won't be able to understand if they are welcoming or threatening.
	attr.shortdesc:at the entrance of a tunnel
	verb:sw
		goto:mountainpath
	verb:d
		goto:minetunnel
	attr.exits:[down,southwest]
room:minetunnel
	name:Mine tunnel
	image:png\tminetunnel.jpg
	desc:The @tunnels seem to go on forever, descending gently down the mountain. From time to time you'll see @crossroads with @markings you can't understand, so you're not sure which way to go. 
	verb:u
		goto:mineentrance
	verb:se
		goto:trolllaborary
	verb:e
	verb:w
	verb:n
	verb:s
		goto:minetunnel
	attr.exits:[up,east,south,west,north]
room:trolllaborary
	name:Forge
	image:png\laboratory.jpg
	desc:This large @room seems to be the heart of the entire tunnel system. There are @inscriptions and complex @drawings on the @walls, a kind of @forge in the centre and, on one side, atop a @stand, a strange elongated @instrument.
	verb:nw
		goto:minetunnel
	attr.shortdesc:in a forge
	attr.exits:[northwest]
